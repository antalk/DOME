;------------------------------------
;               DOME
;
; (c) 1998 PARAGON Productions
;
;             DOME.COM
;
;------------------------------------

	ORG	&H0100
ST:
	JP	START

	DB	&H0D	; type dome.com

	DB	"DOME (c) 1998 PARAGON Productions",&H0D,&H0A
	DB	&H1A	; Einde txt

	ORG	&HC000	; after bios

START:
	CALL	INIT_SCR	; SCREEN 0-80  + ROODE BALK  
	LD	HL,FILE0	; DOME BIOS 
	LD	A,0	; header 0 
	LD	BC,&H0EFF	;  ! > 16 Kb 
	LD	DE,&H0100	; adr 
	CALL	LOAD_FILE

	LD	HL,INT_DOS
	CALL	GET_INT

	LD	HL,RET
	CALL	PUT_TXT

	CALL	CHK_MSX	; MSX 2 OF HIGHER  

	LD	HL,FILE1
	LD	A,0
	LD	BC,&H3FFF
	LD	DE,&H4000
	CALL	LOAD_FILE

	LD	HL,&H4000	; ADR IN RAM  
	LD	A,0	; PAGE (VRAM IN 64 KB) 
	LD	DE,&H8000	; DE IN VRAM 
	CALL	POP.UP_START

	LD	HL,FILE2
	LD	A,0
	LD	BC,&H3FFF
	LD	DE,&H4000
	CALL	LOAD_FILE

	LD	HL,&H4000	; ADR IN RAM   
	LD	A,1	; PAGE (VRAM IN 64 KB)  
	LD	DE,&H0000	; DE IN VRAM  
	CALL	POP.UP_START

	LD	HL,FILE3
	LD	A,0
	LD	BC,&H3FFF
	LD	DE,&H4000
	CALL	LOAD_FILE

	LD	HL,&H4000	; ADR IN RAM   
	LD	A,1	; PAGE (VRAM IN 64 KB)  
	LD	DE,&H8000	; DE IN VRAM   
	CALL	POP.UP_START

	LD	HL,FILE4	; DOME Engine 
	LD	A,0	; header 0 
	LD	BC,&H4FFF	;  ! > 16 Kb 
	LD	DE,&H3000	; adr 
	CALL	LOAD_FILE

	LD	HL,FILE5	; Tank tabel 
	LD	A,0
	LD	BC,&H1FFF
	LD	DE,&H1000
	CALL	LOAD_FILE

	LD	HL,FILE6	; VELD
	LD	A,7	; is nog een BIN file
	LD	BC,&H3FFF
	LD	DE,&H8000	; van 8000-bfff ???
	CALL	LOAD_FILE

	HALT
	HALT

	CALL	SET_SCREEN5

	LD	HL,COLOR_DOME_GAME	; restore color  
	CALL	PUT_COLOR

	LD	A,(&HF3E0)	; screen5,2  
	OR	&B00000010
	LD	(&HF3E0),A
	DI
	OUT	(&H99),A
	LD	A,129	; REG 1   
	OUT	(&H99),A
	EI

	LD	HL,SPRTAB	; maak sprites  
	CALL	MAK_SPR

	LD	HL,COPY_1_0
	CALL	PUTBLK

	DI		; >>> ????  
	CALL	&H3000	; GO GAME !

	DI
	LD	HL,INT_DOS
	CALL	PUT_INT
	CALL	SET_SCREEN0
	EI

	LD	C,0	; officieel !!!!!
	CALL	DOS
	RET

;======================================

INIT_SCR:
	LD	A,80	; nog geen BIOS voorhanden
	LD	(&HF3AE),A
	LD	A,0	; SCREEN 0   
	LD	IY,(&HFCC1)
	LD	IX,&H5F
	CALL	&H1C

	LD	HL,2048
	LD	B,10
INIT_SCR_LOOP:
	LD	A,255
	LD	IY,(&HFCC1)
	LD	IX,&H4D
	CALL	&H1C
	INC	HL
	DJNZ	INIT_SCR_LOOP

	DI
	LD	A,15*16+8
	OUT	(&H99),A
	LD	A,128+12
	OUT	(&H99),A

	LD	A,16*5
	OUT	(&h99),A
	LD	A,128+13
	OUT	(&h99),A
	EI

	RET
CHK_MSX:
	LD	HL,TXT_MSX_TYPE
	CALL	PUT_TXT

	LD	HL,TXT_MSX_1
	OR	A
	CALL	Z,PUT_TXT

	LD	HL,&H002D
	LD	A,(&HFCC1)
	CALL	&H0C

	LD	HL,TXT_MSX_2
	CP	1
	JP	Z,PUT_TXT

	LD	HL,TXT_MSX_2P
	CP	2
	JP	Z,PUT_TXT

	LD	HL,TXT_MSX_TR
	CP	3
	JP	Z,PUT_TXT

	LD	HL,TXT_MSX_ON
	JP	PUT_TXT

;------------------------------------
DOSAGAIN:
	JP	EXIT_ON_ERR

	RET
;-------------------------------------

POP.UP_START:
	LD	(PAGE),A
	LD	(ADRES),DE
	LD	(DATA),HL

	XOR	A

POP.UP:	ADD	A
	ADD	A
	ADD	A
	LD	E,A
	LD	D,0
	LD	IX,POPTBL
	ADD	IX,DE	; [IX]=POPTBL+8*[A]  

	LD	A,I	; IFF to P/V-flag  
	LD	A,-1
	JP	PE,POP.DI
	XOR	A
POP.DI:	LD	(POPIFF),A	; Interrupt status  (0: DI; -1: EI)  

	LD	A,(IX+1)
	LD	(POPPAG),A	; VDP-page (64 kB)  

	LD	L,(IX+2)
	LD	H,(IX+3)	; Source address in [HL]  
	LD	E,(HL)
	INC	HL
	LD	D,(HL)	; Length in [DE]  
	LD	L,(IX+4)
	LD	H,(IX+5)	; Dest. address in [HL]  
	ADD	HL,DE
	LD	(POPEND),HL	; Set end address  

	LD	L,(IX+4)
	LD	H,(IX+5)	; Dest. address in [HL]  
	CALL	POP.WR	; Set VDP to write  

	LD	L,(IX+2)
	LD	H,(IX+3)	; Source address in [HL]  
	LD	DE,&H04
	ADD	HL,DE	; Skip 4 bytes (the 2 addresses)  
	LD	BC,&H1098
	OTIR		; Write 1st 16 bytes in VRAM  

	LD	E,&H80
	EXX
	LD	L,(IX+4)
	LD	H,(IX+5)	; Dest. address in [HL]  
	LD	DE,&H10
	ADD	HL,DE	; Skip 16 bytes  
POP.00:	LD	A,(POPEND+1)
	CP	H
	JR	NZ,POP.01
	LD	A,(POPEND)
	CP	L
	RET	C
	RET	Z	; Return when (POPEND)<=[HL]  
POP.01:	EXX
	RLC	E
	JR	NC,POP.02
	LD	D,(HL)
	INC	HL
POP.02:	RLC	D
	LD	A,(HL)
	INC	HL
	JR	C,POP.03
	EXX

	CALL	POP.WR
	OUT	(&H98),A	; Write in VRAM  
	INC	HL
	JR	POP.00
;
POP.03:	BIT	7,A
	JR	NZ,POP.04
	EXX
	LD	D,&H00
	LD	E,A
	EXX
	XOR	A
	JR	POP.05
;
POP.04:	BIT	6,A
	JR	NZ,POP.06
	RES	7,A
	LD	BC,&H0400
POP.07:	RLC	E
	JR	NC,POP.08
	LD	D,(HL)
	INC	HL
POP.08:	RLC	D
	RLA
	RL	C
	DJNZ	POP.07
	ADD	A,&H80
	EXX
	LD	E,A
	EXX
	LD	A,C
	ADC	A,&H00
	EXX
	LD	D,A
	EXX
	LD	A,&H01
	JR	POP.05
;
POP.06:	AND	&H3F
	SRL	A
	EXX
	LD	D,A
	EXX
	LD	A,(HL)
	INC	HL
	RRA
	EXX
	LD	E,A
	EXX
	LD	A,&H02
	JR	NC,POP.09
	LD	C,&H01
	JR	POP.0A
;
POP.05:	LD	C,A
	INC	A
	RLC	E
	JR	NC,POP.0B
	LD	D,(HL)
	INC	HL
POP.0B:	RLC	D
	JR	NC,POP.09
POP.0A:	INC	A
	RLC	E
	JR	NC,POP.0C
	LD	D,(HL)
	INC	HL
POP.0C:	RLC	D
	JR	NC,POP.09
	INC	A
	RLC	E
	JR	NC,POP.0D
	LD	D,(HL)
	INC	HL
POP.0D:	RLC	D
	JR	NC,POP.09
	LD	A,&H02
POP.0E:	RLC	E
	JR	NC,POP.0F
	LD	D,(HL)
	INC	HL
POP.0F:	RLC	D
	JR	NC,POP.10
	INC	A
	CP	&H07
	JR	NZ,POP.0E
POP.10:	LD	B,A
	LD	A,&H01
POP.11:	RLC	E
	JR	NC,POP.12
	LD	D,(HL)
	INC	HL
POP.12:	RLC	D
	RLA
	DJNZ	POP.11
	ADD	A,C
POP.09:	EXX

	PUSH	AF
	PUSH	HL

	LD	(POPXXX),HL	; write address in (popxxx)  
	SCF
	SBC	HL,DE	; read address in [HL]  
	LD	C,&H98
	CALL	POP.13	; Simulate 'LDIR' in VRAM  

	POP	HL
	POP	AF
	LD	C,A
	LD	B,0
	INC	BC
	ADD	HL,BC	; [HL#]=[HL]+[A]+1  
	JP	POP.00

POP.13:	LD	B,A
	INC	DE
	LD	A,D
	OR	A
	JP	NZ,POP.15	; [DE]>255 = [D]>0: no overlap  

	LD	A,B	; [B]=len-1  
	SUB	E	; [E]=x  
	JP	C,POP.15	; [B]<[E]: no overlap  (len<=x)  

	INC	B
	PUSH	BC
	LD	B,E
	CALL	POP.RD	; read address still in [HL]  
	LD	HL,POPBUF
	INIR		; read x bytes in VRAM  

	LD	C,A
	LD	B,0
	INC	BC	; [BC]=len-x  
	LD	HL,POPBUF
	ADD	HL,DE
	EX	DE,HL	; [DE]=POPBUF+x  
	LD	HL,POPBUF	; [HL]=POPBUF  
	LDIR

POP.14:	POP	BC
	LD	HL,(POPXXX)
	CALL	POP.WR
	LD	HL,POPBUF
	OTIR		; write bytes in VRAM  
	RET

POP.15:	INC	B
	PUSH	BC
	CALL	POP.RD	; read address still in [HL]  
	LD	HL,POPBUF
	INIR		; read bytes in VRAM  
	JP	POP.14

POP.WR:	PUSH	BC	; Set write address  
	LD	C,A
	LD	A,(POPPAG)
	LD	B,A
	LD	A,H
	AND	&B11000000
	OR	B
	RLCA
	RLCA
	DI
	OUT	(&H99),A
	LD	A,&H8E
	OUT	(&H99),A
	LD	A,L
	LD	A,L
	OUT	(&H99),A
	LD	A,H
	AND	&H3F
	OR	&H40
	OUT	(&H99),A

	LD	A,(POPIFF)
	OR	A
	LD	A,C
	POP	BC
	RET	Z	; Restore int. status  
	EI
	RET

POP.RD:	PUSH	BC	; Set read address  
	LD	C,A
	LD	A,(POPPAG)
	LD	B,A
	LD	A,H
	AND	&B11000000
	OR	B
	RLCA
	RLCA
	DI
	OUT	(&H99),A
	LD	A,&H8E
	OUT	(&H99),A
	LD	A,L
	LD	A,L
	OUT	(&H99),A
	LD	A,H
	AND	&H3F
	OUT	(&H99),A

	LD	A,(POPIFF)
	OR	A
	LD	A,C
	POP	BC
	RET	Z	; Restore int. status  
	EI
	RET


;-------------------------------------


;-----------------------------------------------
PUTBLK:

	DI		; vdp klaar ?      
	LD	A,2	; waarde 2 in reg 15      
	OUT	(&H99),A
	LD	A,&H8F	; execute      
	OUT	(&H99),A
	NOP
	NOP		; wacht op vdp      
PUTBLK_WAIT:
	IN	A,(&H99)	;lees status      
	RRA		; is CE nog 1 dan C is loop      
	JR	C,PUTBLK_WAIT

	XOR	A	; status reg. op 0      
	OUT	(&H99),A
	LD	A,&H8F
	OUT	(&H99),A

	LD	A,&H20	; register 32      
	OUT	(&H99),A	; als control register      
	LD	A,&H91	;in reg 17      
	OUT	(&H99),A
	LD	BC,&H0F9B	; 15 bytes naar port 9b      
	OTIR
	EI

	RET

MAK_SPR:
	DI
	LD	A,1
	OUT	(&H99),A
	LD	A,&H8E
	OUT	(&H99),A
	LD	A,255
	OUT	(&H99),A
	LD	A,&H37
	OUT	(&H99),A
	LD	HL,SPRTAB

	LD	B,13
LOOPIE:
	PUSH	BC
	LD	BC,&H2098
	OTIR
	POP	BC
	DJNZ	LOOPIE

	EI
	RET

PUT_COLOR
	DI
	XOR	A
	OUT	(&H99),A
	LD	A,16+128
	OUT	(&H99),A

	LD	BC,&H209A
	OTIR
	EI
	RET

;================= LOADING

INT_DOS:	DW	0

TXT_GFX_LOAD:	DB	"DOME-init : loading :",&H1D

TXT_PUNT:	DB	".",&H1D

TXT_READY:	DB	"READY.",&H0A,&H0D,&H1D

TXT_LACH_DOME:DB	"Launching Dome......................"
	DB	&H0A,&H0D,&H1D

SPRTAB:
	DB	254,130,132,136,144,160,192,0
	DB	0,0,0,0,0,0,0,0
	DB	0,0,0,0,0,0,0,0
	DB	0,0,0,0,0,0,0,0

	DB	&HFF,&H80,&H80,&H80,&H80,&H80,&H80,&H80
	DB	&H80,&HFF,0,0,0,0,0,0
	DB	&HC0,&H40,&H40,&H40,&H40,&H40,&H40,&H40
	DB	&H40,&HC0,0,0,0,0,0,0

	DB	&H20,&H40,&HA0,&H10,&H08,0,0,0
	DB	0,0,0,&H08,&H10,&HA0,&H40,&H20
	DB	&H04,&H02,&H05,&H08,&H10,0,0,0
	DB	0,0,0,&H10,&H08,&H05,&H02,&H04

	; pijl omhoog     
	DB	&H01,&H02,&H04,&H08,&H0e,&H02,&H02,&H03
	DB	0,0,0,0,0,0,0,0
	DB	&H80,&H40,&H20,&H10,&H70,&H40,&H40,&Hc0
	DB	0,0,0,0,0,0,0,0
	; pijl rechts     
	DB	0,0,0,0,&H18,&H14,&HF2,&H81
	DB	&H81,&HF2,&H14,&H18,0,0,0,0
	DB	0,0,0,0,0,0,0,0
	DB	0,0,0,0,0,0,0,0
	; pijl omlaag     
	DB	&H03,&H02,&H02,&H0E,&H08,&H04,&H02,&H01
	DB	0,0,0,0,0,0,0,0
	DB	&HC0,&H40,&H40,&H70,&H10,&H20,&H40,&H80
	DB	0,0,0,0,0,0,0,0
	; pijl links     
	DB	0,0,0,0,&H18,&H24,&H47,&H81
	DB	&H81,&H47,&H24,&H18,0,0,0,0
	DB	0,0,0,0,0,0,0,0
	DB	0,0,0,0,0,0,0,0
	; attack rondje     
	DB	&H07,&H18,&H20,&H40,&H40,&H80,&H83,&H83
	DB	&H80,&H40,&H40,&H20,&H18,&H07,0,0
	DB	&H80,&H60,&H10,&H08,&H08,&H04,&H04,&H04
	DB	&H04,&H08,&H08,&H10,&H60,&H80,0,0
	; build blokje     
	DB	&H88,&H11,&H22,&H44,&H88,&H11,&H22,&H44
	DB	&H88,&H11,&H22,&H44,&H88,&H11,&H22,&H44
	DB	&H88,&H11,&H22,&H44,&H88,&H11,&H22,&H44
	DB	&H88,&H11,&H22,&H44,&H88,&H11,&H22,&H44
	; multiple select 1     
	DB	&HF0,&H80,&H80,&H80,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	; multiple select 2     
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H80,&H80,&H80,&Hf0
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	; multiple select 3     
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H0f,&H01,&H01,&H01,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	; multiple select 4     
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H00,&H00,&H00,&H00
	DB	&H00,&H00,&H00,&H00,&H01,&H01,&H01,&H0f

COLOR_DOME_GAME:
	DB	#00,#00,#62,#04,#52,#03,#50,#02
	DB	#23,#02,#20,#01,#53,#04,#11,#01
	DB	#00,#04,#45,#04,#40,#01,#72,#05
	DB	#04,#01,#06,#02,#00,#00,#77,#07

COPY_1_0:	DB	0,0,0,1
	DB	0,0,0,0
	DB	0,1,212,0
	DB	0,0,&HD0


POPPAG:	DB	0
POPIFF:	DB	0
POPEND:	DW	0
POPXXX:	DW	0
POPBUF:	DS	256

POPTBL:
	DB	7	; Mapperbank ( niet gebruikt ? ) 
PAGE:	DB	0	; VDP-page (64 kB or b16 of VRAM-add   
DATA:	DW	0	; Source address (in RAM)   
ADRES:	DW	0	; Dest. address (in VRAM)   
	DW	0	; End address (in VRAM, is calculated  

;---------------------------------

DOME_LIB0:	DB	"DOME    000"

FILE0:	DB	"BIOS    DAT"

FILE1:
	DB	"BALKEN4 ZOP"
FILE2:
	DB	"UNITTOT ZOP"
FILE3:
	DB	"PAGE5   ZOP"
FILE4:
	DB	"DOME    DAT"
FILE5:
	DB	"TANKTAB DAT"
FILE6:
	DB	"VELD    DAT"

;============================== TEXT'EN

RET:	DB	&H0A,&H0D,&H1D
TXT:	DB	"    ",&H0A,&H0D,&H1D

TXT_ERR:	DB	"Please install the specified device before loading "
	DB	"DOME...PRESS A KEY TO RESUME.",&H0D,&H1D

TXT_DOME_INIT:DB	"DOME_init : DOME v0.7c ",&H0A,&H0D,&H1D


;===========  MSX_TYPE TXT'S

TXT_MSX_TYPE:	DB	"SYS_init : ",&H1D
TXT_MSX_1:	DB	"MSX-1 Found.",&H0A,&H0D,&H1D
TXT_MSX_2:	DB	"MSX-2 Found.",&H0A,&H0D,&H1D
TXT_MSX_2P:	DB	"MSX-2+ Found.",&H0A,&H0D,&H1D
TXT_MSX_TR:	DB	"MSX-TURBO-R Found.",&H0A,&H0D,&H1D
TXT_MSX_ON:	DB	"MSX-3 Found ,let us know it's name.",&H0A,&H0D,&H1D

;========== MOUSE TXT'S

TXT_MOUSE_SE:	DB	"Mouse_init: Scaning Game Ports...",&H0A,&H0D,&H1D
TXT_MOUSE_YES:DB	"            Mouse found on port : "
TXT_MOUSE_PORT:	DB	" ",&H0A,&H0D,&H1D
TXT_MOUSE_NO:	DB	"            No mouse found",&H0A,&H0D,&H1D
TXT_MOUSE_ERR:DB	"You need a mouse connected on port 1 or 2 to "
	DB	"run DOME.",&H0A,&H0D,&H1D

;============  DISK ERROR TXT'S

TXT_D_NO_BIN:	DB	"Error : the specified file is corruted..."
	DB	&H0A,&H0D,&H1D

;===================== EXIT DOME

TXT_THANKS:	DB	"Thanks for playing Dome."
	DB	&H0A,&H0D,&H1D

	INCLUDE	3	; library functions 

EI:
